plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlin.android
    alias libs.plugins.google.secrets
    alias libs.plugins.jfrog
    alias libs.plugins.meaven.local
}

android {
    namespace = "io.kommunicate"

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 21
        compileSdk 34
        targetSdkVersion 35
        versionCode 1
        versionName "2.13.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "KOMMUNICATE_VERSION", "\"" + versionName + "\""
        buildConfigField "String", "CHAT_SERVER_URL", '"https://chat.kommunicate.io"'
        buildConfigField "String", "API_SERVER_URL", '"https://api.kommunicate.io"'
        buildConfigField "String", "GOOGLE_API_SERVER_URL", '"https://maps.googleapis.com/maps/api/staticmap?"'
        buildConfigField "String", "MQTT_URL", '"tcp://socket.kommunicate.io:1883"'
        buildConfigField "String", "HELPCENTER_URL", '"https://helpcenter.kommunicate.io"'
        buildConfigField "String","EU_CHAT_SERVER_URL",'"https://chat-eu.kommunicate.io"'
        buildConfigField "String", "EU_API_SERVER_URL", '"https://api-eu.kommunicate.io"'
        buildConfigField "String", "MQTT_URL_EU", '"tcp://socket-eu.kommunicate.io:1883"'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation libs.core.ktx
    implementation libs.android.database.sqlcipher
    api libs.org.eclipse.paho.client.mqttv3
    api libs.localbroadcastmanager
    api libs.appcompat
    api libs.gson
    api libs.silicompressor
    api libs.sentry.android
    api(libs.isoparser) {
        exclude(
                group : "org.aspectj",
                module: "aspectjrt"
        )
    }
}

secrets {
    defaultPropertiesFileName = "release.secrets.properties"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}
task javadoc(type: Javadoc) {
    failOnError false // add this line
    source = android.sourceSets.main.java.srcDirs
    //source = android.sourceSets.main.allJava
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}
publishing {
    publications {
        aar(MavenPublication) {
            groupId = 'io.kommunicate.sdk'
            artifactId = project.getName()
            version = android.defaultConfig.versionName
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
//            artifact(javadocJar)
            artifact(sourcesJar)
            //generate POM
            pom.withXml {
                def node = asNode()
                def dependenciesNode = node.appendNode('dependencies')
                def dependencies = { Dependency dep ->
                    if (dep.group == null || dep.name == null || dep.name == "unspecified") {
                        return
                    }
                    def dependencyGroup
                    if (dep.group == "") { //
                        dependencyGroup = groupId
                    } else {
                        dependencyGroup = dep.group
                    }
                    def dependencyVersion
                    //"fix" for compile projects
                    if (dep.version == "unspecified" || dep.version == null) {
                        dependencyVersion = version
                    } else {
                        dependencyVersion = dep.version
                    }
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependencyGroup)
                    dependencyNode.appendNode('artifactId', dep.name)
                    dependencyNode.appendNode('version', dependencyVersion)
                    if (!dep.transitive) {
                        // If this dependency is transitive, we should force exclude all its dependencies them from the POM
                        def exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
                        exclusionNode.appendNode('groupId', '*')
                        exclusionNode.appendNode('artifactId', '*')
                    } else if (!dep.properties.excludeRules.empty) {
                        // Otherwise add specified exclude rules
                        def exclusionsNode = dependencyNode.appendNode('exclusions')
                        dep.properties.excludeRules.each { ExcludeRule rule ->
                            def exclusion = exclusionsNode.appendNode('exclusion')
                            exclusion.appendNode('groupId', rule.group ?: '*')
                            exclusion.appendNode('artifactId', rule.module ?: '*')
                        }
                    }
                }
                // List all dependencies and write to POM
                configurations.api.getAllDependencies().each dependencies
                configurations.implementation.getAllDependencies().each dependencies
            }
        }
    }
}
/*
artifactoryPublish {
   publications(publishing.publications.aar)
}
*/
