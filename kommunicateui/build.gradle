plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlin.android
    alias libs.plugins.jfrog
    alias libs.plugins.meaven.local
}

android {
    namespace = "io.kommunicate.ui"

    lintOptions {
        abortOnError false
    }
    defaultConfig {
        compileSdk 34
        targetSdkVersion 35
        minSdkVersion 21
        versionCode 1
        versionName "2.13.3"
        buildToolsVersion = '34.0.0'
        consumerProguardFiles 'proguard-rules.txt'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        buildConfigField "String", "KOMMUNICATE_UI_VERSION", "\"" + versionName + "\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api project(':kommunicate')
//    api 'io.kommunicate.sdk:kommunicate:2.10.4-temp'
    api libs.firebase.messaging
    api libs.play.services.maps
    api libs.gms.play.services.location
    api libs.circleimageview
    api libs.glide
    api libs.cardview
    api libs.material
    implementation libs.browser
    api libs.swiperefreshlayout
    implementation libs.media3.exoplayer
    implementation libs.media3.ui
    implementation libs.flexbox
    implementation libs.constraintlayout
    implementation libs.core.ktx
    implementation libs.multidex
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}
task javadoc(type: Javadoc) {
    failOnError false // add this line
    source = android.sourceSets.main.java.srcDirs
    //source = android.sourceSets.main.allJava
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}
publishing {
    publications {
        aar(MavenPublication) {
            groupId = 'io.kommunicate.sdk'
            artifactId = project.getName()
            version = android.defaultConfig.versionName
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
//            artifact(javadocJar)
            artifact(sourcesJar)
            //generate POM
            pom.withXml {
                def node = asNode()
                def dependenciesNode = node.appendNode('dependencies')
                def dependencies = { Dependency dep ->
                    if (dep.group == null || dep.name == null || dep.name == "unspecified") {
                        return
                    }
                    def dependencyGroup
                    if (dep.group == "Kommunicate-Android-Chat-SDK") { //
                        dependencyGroup = groupId
                    } else {
                        dependencyGroup = dep.group
                    }
                    def dependencyVersion
                    //"fix" for compile projects
                    if (dep.version == "unspecified" || dep.version == null) {
                        dependencyVersion = version
                    } else {
                        dependencyVersion = dep.version
                    }
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependencyGroup)
                    dependencyNode.appendNode('artifactId', dep.name)
                    dependencyNode.appendNode('version', dependencyVersion)
                    if (!dep.transitive) {
                        // If this dependency is transitive, we should force exclude all its dependencies them from the POM
                        def exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
                        exclusionNode.appendNode('groupId', '*')
                        exclusionNode.appendNode('artifactId', '*')
                    } else if (!dep.properties.excludeRules.empty) {
                        // Otherwise add specified exclude rules
                        def exclusionsNode = dependencyNode.appendNode('exclusions')
                        dep.properties.excludeRules.each { ExcludeRule rule ->
                            def exclusion = exclusionsNode.appendNode('exclusion')
                            exclusion.appendNode('groupId', rule.group ?: '*')
                            exclusion.appendNode('artifactId', rule.module ?: '*')
                        }
                    }
                }
                // List all dependencies and write to POM
                configurations.api.getAllDependencies().each dependencies
                configurations.implementation.getAllDependencies().each dependencies
            }
        }
    }
}

/*
artifactoryPublish {
   publications(publishing.publications.aar)
}
*/
